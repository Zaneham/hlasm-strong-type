.*
.* This macro is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License
.* or (at your option) any later version.
.* The license text is available at the following internet addresses:
.* - http://www.bixoft.com/english/gpl.htm
.* - http://fsf.org
.* - http://opensource.org
.*
.* This macro is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
.* See the GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License
.* along with this program; if not, write to either of the following:
.* the Free Software Foundation, Inc.      B.V. Bixoft
.* 59 Temple Place, Suite 330              Rogge 9
.* Boston, MA 02111-1307                   7261 JA Ruurlo
.* United States of America                The Netherlands
.*
.*                                         e-mail: bixoft@bixoft.nl
.*                                         phone : +31-6-22755401
.*
.**********************************************************************
.*
.* Bixoft eXtended Assembly language
.* Licensed material - Property of B.V. Bixoft
.*
.* This macro can be licensed or used on an as-is basis.
.* No warranty, neither implicit nor explicit, is given.
.* It remains your own responsibility to ensure the correct
.* working of any program using this macro.
.*
.* Suggestions for improvement are always welcome at
.* http://www.bixoft.com  or mail to  bixoft@bixoft.nl
.*
.* (C) Copyright B.V. Bixoft, 1999
.**********************************************************************
         MACRO
.*
.* Create a literal pool. This macro replaces the normal LTORG
.* instruction. The required OPSYN is issued by the PGM macro.
.*
&LABEL   LTORG
.**********************************************************************
.*
.*       IMPORTANT NOTICE
.*       ========= ======
.*
.* Code below checks whether 'USER' accepted the terms and conditions
.* of the license for the BXA macro library. This code is to be treated
.* as part of the Copyright Notice and therefore may not be changed
.* or disabled in any way.
.*
.**********************************************************************
         GBLA  &BXA_RC                 * Returncode from CHKLIC
         CHKLIC LTORG                  * Check license acceptance
         AIF   (&BXA_RC NE 0).MEND
.**********************************************************************
.*
.* End of special code that is part of the Copyright Notice
.*
.**********************************************************************
.*
.* Declare variables
         GBLC  &BXA_EXQ_I(50)          * Instructions used by EXQ
         GBLC  &BXA_EXQ_OPS(50)        * Operands for the instructions
         GBLA  &BXA_EXQ_LAST           * Pointer to last expanded
         GBLC  &BXA_SUBR               * Current subroutine
         GBLB  &SP_SHOWALL             * Print option from PGM macro
         LCLC  &_PRINT                 * Print option for PRINT
         LCLC  &_PROPT                 * Print option for PUSH/POP
         LCLA  &I                      * Index for BXA_EXQ_I / _OPS
         LCLC  &EXQI                   * Entry from BXA_EXQ_I
         LCLC  &EXQOPS                 * Entry from BXA_EXQ_OPS
.*
.* Check that we're not in the midst of a subroutine
         AIF   ('&BXA_SUBR' EQ '*MAIN').NOERR1
.ERR1    MNOTE 8,'Missing ENDSR statement'
.NOERR1  ANOP
.*
.* Check the number of arguments
         AIF   (N'&SYSLIST EQ 0).NOERR2
&I       SETA  0
.LOOP0   ANOP
&I       SETA  &I+1
         AIF   (&I GT N'&SYSLIST).LOOP0OK
         AIF   (K'&SYSLIST(&I) EQ 0).LOOP0
.ERR2A   MNOTE 4,'No operands allowed on LTORG: ignored'
.LOOP0OK ANOP
.NOERR2  ANOP
.*
.* Generate code
&LABEL   LABEL H                       * Align on halfword
.*
.* Create all EXQ-instructions with their labels
         AIF   (N'&BXA_EXQ_I LE &BXA_EXQ_LAST).NOLOOP1 * Nothing new?
**********************************************************************
*
* Instructions for use with EXexute
*
**********************************************************************
&I       SETA  &BXA_EXQ_LAST           * I indexes BXA_EXQ
.LOOP1   ANOP  ,                       *
&I       SETA  &I+1                    * Point to next entry
         AIF   (&I GT N'&BXA_EXQ_I).LOOP1OK * End of loop
&EXQI    SETC  '&BXA_EXQ_I(&I)'        * Extract opcode
&EXQOPS  SETC  '&BXA_EXQ_OPS(&I)'      * Extract operands
_EXQ&I   &EXQI &EXQOPS                 * EXecutable instruction
         AGO   .LOOP1                  * Go process next one
.LOOP1OK ANOP  ,                       *
&BXA_EXQ_LAST  SETA &I-1               * Set ptr to last expanded
.NOLOOP1 ANOP  ,                       *
.*
.* Set listing options
&_PRINT  SETC  'ON'                    * SHOWALL: show PRINT lines
         AIF   (&SP_SHOWALL).PRINTOK   * SHOWALL: show PUSH/POP lines
&_PRINT  SETC  'OFF,NOPRINT'           * Not ALL: omit PRINT lines
&_PROPT  SETC  ',NOPRINT'              * Not ALL: omit PUSH/POP lines
.PRINTOK ANOP
.*
.* Insert real literal pool
**********************************************************************
*
* Literals
*
**********************************************************************
         _LTORG
.*
.MEND    MEND
