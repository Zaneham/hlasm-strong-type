.*
.* This macro is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License
.* or (at your option) any later version.
.* The license text is available at the following internet addresses:
.* - http://www.bixoft.com/english/gpl.htm
.* - http://fsf.org
.* - http://opensource.org
.*
.* This macro is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
.* See the GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License
.* along with this program; if not, write to either of the following:
.* the Free Software Foundation, Inc.      B.V. Bixoft
.* 59 Temple Place, Suite 330              Rogge 9
.* Boston, MA 02111-1307                   7261 JA Ruurlo
.* United States of America                The Netherlands
.*
.*                                         e-mail: bixoft@bixoft.nl
.*                                         phone : +31-6-22755401
.*
.**********************************************************************
.*
.* Bixoft eXtended Assembly language
.* Licensed material - Property of B.V. Bixoft
.*
.* This macro can be licensed or used on an as-is basis.
.* No warranty, neither implicit nor explicit, is given.
.* It remains your own responsibility to ensure the correct
.* working of any program using this macro.
.*
.* Suggestions for improvement are always welcome at
.* http://www.bixoft.com  or mail to  bixoft@bixoft.nl
.*
.* (C) Copyright B.V. Bixoft, 1999
.**********************************************************************
         MACRO
.*
.* Define internal macro for EXSR
.*
         EXSR0  ,                      *
.*
.* Define variables
         GBLB  &BXA_EXSR               * EXSR expanded before?
.*
.* Define internal macro on the first invocation.
         AIF   (&BXA_EXSR).MEND
&BXA_EXSR SETB 1
         PUSH  PRINT,NOPRINT
         PRINT OFF,NOPRINT
.*
         MACRO
         EXSR_ADD_ENTRY &NAME,&TYPE    * Name and type of subroutine
         GBLC  &BXA_SRUNAM(50)         * Used subroutine names
         GBLA  &BXA_SRUCT(50)          * Subroutine invocation count
         GBLC  &BXA_SRUASC(50)         * Used subroutine ASC-modes
         GBLB  &BXA_SRUSVC(50)         * Used subroutine SVC-modes
         GBLC  &BXA_SRDNAM(50)         * Defined subroutine names
         GBLC  &BXA_SRDTYP(50)         * Defined subroutine types
         GBLA  &BXA_SRNAML             * Max subr name length
         GBLC  &BXA_SR_CALLER(50)      * Calling macro-names
         GBLC  &BXA_SR_CALLED(50)      * Called macro-names
         GBLC  &BXA_SUBR               * Current subr name or *MAIN
         GBLC  &BXA_USEC_ROUT(50)      * Routines for SCOPE=CALLED
         GBLC  &BXA_USEC_ARGL(50)      * Labels for SCOPE=CALLED
         GBLC  &BXA_USEC_ARG1(50)      * DSECTs for SCOPE=CALLED
         GBLC  &BXA_USEC_ARG2(50)      * Bases for SCOPE=CALLED
         GBLB  &BXA_SVCMODE            * Current SVC-mode
         GBLC  &SYSASCE                * Current ASC-mode
         LCLA  &I,&J                   * Index into BXA_SRUNAM/SRUCT
.*
.* Update subroutine usage count
&I       SETA  0                       * Init loop
.LOOP1   ANOP
&I       SETA  &I+1                    * Point to next entry
         AIF   (&I GT N'&BXA_SRUNAM).LOOP1AD * End of array: add entry
         AIF   ('&BXA_SRUNAM(&I)' EQ '&NAME').LOOP1IN * Increment use
         AGO   .LOOP1                  * Go process next entry
.LOOP1AD ANOP  ,                       * Add entry to UNAM
&BXA_SRUCT(&I) SETA 1                  * Use count is now 1
&BXA_SRUSVC(&I) SETB (&BXA_SVCMODE)    * Copy current SVC-mode
&BXA_SRUASC(&I) SETC '&SYSASCE'        * Copy current ASC-mode
&BXA_SRUNAM(&I) SETC '&NAME'           * Name into empty entry
         AIF   (K'&NAME LE &BXA_SRNAML).LOOP1OK * Check name length
&BXA_SRNAML SETA K'&NAME               * Increase max name length
         AIF   (K'&NAME GE 5).LOOP1OK  * Max name length must at least
&BXA_SRNAML SETA 5                     *   equal 5 (*MAIN)
         AGO   .LOOP1OK                * And we're done
.* Entry found: update count and check interface
.LOOP1IN ANOP  ,                       * Increment use count
&BXA_SRUCT(&I) SETA &BXA_SRUCT(&I)+1   *           by one
.*
         AIF   (&BXA_SRUSVC(&I) EQ &BXA_SVCMODE).NOERR0A
.ERR0A   MNOTE 8,'Interface error: &NAME called both in SVC and in prob*
               lem mode'
.NOERR0A ANOP
         AIF   ('&BXA_SRUASC(&I)' EQ '&SYSASCE').NOERR0B
         AIF   ('&TYPE' EQ 'EXT').NOERR0B * EXT always called in prim.
.ERR0B   MNOTE 8,'Interface error: &NAME called both in Access Register*
                and in Primary mode'
.NOERR0B ANOP
.LOOP1OK ANOP
.*
.* Add entry to subroutine call cross-reference tables
&I       SETA  1+N'&BXA_SR_CALLED
&BXA_SR_CALLER(&I) SETC '&BXA_SUBR'
&BXA_SR_CALLED(&I) SETC '&NAME'
.*
.* If the called subroutine is defined, it must be an INT or EXT subr
&I       SETA  0                       * I indexes into BXA_SRDNAM
.LOOP2   ANOP  ,                       * Loop thru BXA_SRDNAM
&I       SETA  &I+1                    * Point to next entry
         AIF   (&I GT N'&BXA_SRDNAM).LOOP2NF * Not found: skip
         AIF   ('&NAME' NE '&BXA_SRDNAM(&I)').LOOP2 * Continue search
.* Called subr is defined: check its type
         AIF   ('&BXA_SRDTYP(&I)' EQ 'INT').LOOP2OK
         AIF   ('&BXA_SRDTYP(&I)' EQ 'EXT').LOOP2OK
.ERR1A   MNOTE 8,'You cannot EXSR to a subroutine of type &BXA_SRDTYP(&*
               I)'
         AGO   .LOOP2OK
.LOOP2NF ANOP  ,                       * If external subr: add entry
         AIF   ('&TYPE' NE 'EXT').LOOP2OK
.*
.* External subroutine name not found: add entry to arrays
&I       SETA  1+N'&BXA_SRDNAM         * Point to first free entry
&BXA_SRDNAM(&I) SETC '&NAME'           * Add subroutine name
&BXA_SRDTYP(&I) SETC 'EXT'             *   and subroutine type
&BXA_SRDDUP(&I) SETB 0                 * External subr never a dup!
         AIF   (K'&NAME LE &BXA_SRNAML).LOOP2OK * Check name length
&BXA_SRNAML SETA K'&NAME               * Increase max. name length
         AIF   (K'&NAME GE 5).LOOP2OK  * Max. name length must at least
&BXA_SRNAML SETA 5                     *   equal 5 (for *MaIN)
.LOOP2OK ANOP
.*
.* USEs with SCOPE=CALLED must be propagated in the BXA_USEC tables
&I       SETA  0                       * I indexes BXA_USEC_...
.LOOP3   ANOP  ,                       * For all entries
&I       SETA  &I+1                    * Point next entry
         AIF   (&I GT N'&BXA_USEC_ROUT).LOOP3OK * At end: quit loop
         AIF   ('&BXA_USEC_ROUT(&I)' NE '&BXA_SUBR').LOOP3
.* Match: copy to called subroutine, unless such an entry already
.*        exists. &I points to located entry.
&J       SETA  0                       * J is second-search index
.LOOP4   ANOP  ,                       * See if entry exists
&J       SETA  &J+1                    * Point to next entry
         AIF   (&J GT N'&BXA_USEC_ROUT).LOOP4NF * At end: not found
         AIF   ('&BXA_USEC_ROUT(&J)' NE '&NAME').LOOP4
         AIF   ('&BXA_USEC_ARGL(&J)' NE '&BXA_USEC_ARGL(&I)').LOOP4
         AIF   ('&BXA_USEC_ARG1(&J)' NE '&BXA_USEC_ARG1(&I)').LOOP4
         AIF   ('&BXA_USEC_ARG2(&J)' NE '&BXA_USEC_ARG2(&I)').LOOP4
.* Entry to be copied exists for this subroutine: skip copy operation
         AGO   .LOOP3
.* Second search yielded nothing: Add entry for called subroutine
.LOOP4NF ANOP
&J       SETA  N'&BXA_USEC_ROUT+1      * J points to new entry
&BXA_USEC_ROUT(&J) SETC '&NAME'        * Use USE for this subroutine
&BXA_USEC_ARGL(&J) SETC '&BXA_USEC_ARGL(&I)' * USE label parameter
&BXA_USEC_ARG1(&J) SETC '&BXA_USEC_ARG1(&I)' * USE dsect parameter
&BXA_USEC_ARG2(&J) SETC '&BXA_USEC_ARG2(&I)' * USE base parameter
         AGO   .LOOP3
.LOOP3OK ANOP
.*
.MEND    MEND
         POP   PRINT,NOPRINT
.*
.MEND    MEND
