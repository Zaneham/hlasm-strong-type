.*
.* This macro is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License
.* or (at your option) any later version.
.* The license text is available at the following internet addresses:
.* - http://www.bixoft.com/english/gpl.htm
.* - http://fsf.org
.* - http://opensource.org
.*
.* This macro is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
.* See the GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License
.* along with this program; if not, write to either of the following:
.* the Free Software Foundation, Inc.      B.V. Bixoft
.* 59 Temple Place, Suite 330              Rogge 9
.* Boston, MA 02111-1307                   7261 JA Ruurlo
.* United States of America                The Netherlands
.*
.*                                         e-mail: bixoft@bixoft.nl
.*                                         phone : +31-6-22755401
.*
.**********************************************************************
.*
.* Bixoft eXtended Assembly language
.* Licensed material - Property of B.V. Bixoft
.*
.* This macro can be licensed or used on an as-is basis.
.* No warranty, neither implicit nor explicit, is given.
.* It remains your own responsibility to ensure the correct
.* working of any program using this macro.
.*
.* Suggestions for improvement are always welcome at
.* http://www.bixoft.com  or mail to  bixoft@bixoft.nl
.*
.* (C) Copyright B.V. Bixoft, 1999
.**********************************************************************
         MACRO
.*
.* WTO with remote text
.*
&LABEL   RWTO  &TEXT                   * Text for WTO
.*
.* &LABEL specifies an optional label
.* &TEXT  specifies the text for the WTO. Must specify one of the
.*        following:
.*        - Literal text, enclosed in apostrophes
.*        - Fieldname of the field containing the parmlist (MAPWTOPL)
.*        - (reg) pointing to the parmlist (MAPWTOPL)
.*
.**********************************************************************
.*
.*       IMPORTANT NOTICE
.*       ========= ======
.*
.* Code below checks whether 'USER' accepted the terms and conditions
.* of the license for the BXA macro library. This code is to be treated
.* as part of the Copyright Notice and therefore may not be changed
.* or disabled in any way.
.*
.**********************************************************************
         GBLA  &BXA_RC                 * Returncode from CHKLIC
         CHKLIC RWTO                   * Check license acceptance
         AIF   (&BXA_RC NE 0).MEND
.**********************************************************************
.*
.* End of special code that is part of the Copyright Notice
.*
.**********************************************************************
.*
.* Define variables
         GBLA  &BXA_NUMVAL             * Result from CHKREG
         LCLC  &RLBL                   * Remote label
         LCLC  &_REG                   * Register to plist
.*
.* Check TEXT parameter
         AIF   (K'&TEXT EQ 0).ERR1A    *
         AIF   ('&TEXT'(1,1) NE '(').NOERR1
&_REG    SETC  '&TEXT(1)'              *
         CHKREG &_REG,g                * Valid GPR?
         AIF   (&BXA_RC GT 4).ERR1B    *
         AGO   .NOERR1
.ERR1A   MNOTE 8,'Missing text for WTO'
         MEXIT
.ERR1B   MNOTE 8,'&_REG is not a valid general purpose register'
         MEXIT
.NOERR1  ANOP
.*
.* Check nr of arguments
         AIF   (N'&SYSLIST EQ 1).NOERR2
.ERR2A   MNOTE 4,'Expected only 1 argument: remainder ignored'
.NOERR2  ANOP
.*
.* Generate code
         AIF   ('&TEXT'(1,1) EQ '''').GENLIT
         AIF   ('&TEXT'(1,1) EQ '(').GENREG
         AGO   .GENLOC                 *
.*
.GENLIT  ANOP  ,                       * Generate for literal argument
&RLBL    SETC  'RWTO&SYSNDX'           *
&RLBL    RDATA WTO,&TEXT               * Define message text (remote)
&LABEL   L     R1,=AL4(&RLBL)          * Point remote plist
         AGO   .WTO                    *
.*
.GENREG  ANOP  ,                       * Generate for register argument
&LABEL   CPY   R1,&_REG,NOWARN         * Point remote plist
         AGO   .WTO                    *
.*
.GENLOC  ANOP  ,                       * Generate for location argument
&LABEL   LA    R1,&TEXT                * Point remote plist
.*
.WTO     ANOP  ,                       *
         SVC   35                      * Issue WTO
.*
.MEND    MEND
